schema {
    version: 0.1.0
}

compound_task "BeDude" {
    method "Main scared task" {
        subtasks: [IsScared]
    }
    method "Scared by player proximity" {
        preconditions: [mood == Mood::Normal, proximity_alert == true]
        subtasks: [
            BecomeScared,
            BeDude,
        ]
    }
    method "Celebration" {
        preconditions: [coins_collected >= 3]
        subtasks: [
            Celebrate
        ]
    }
    method "Relaxed and wandering" {
        preconditions: [coin_location == None]
        subtasks: [
            PickDestination,
            MoveToNextDestination,
            Chill,
        ]
    }
    method "Chasing a coin" {
        //preconditions: [coin_location != None]
        subtasks: [
            MoveToCoin,
            Pause,
        ]
    }
    method "Pause and go again" {
        subtasks: [Pause, BeDude]
    }
}

compound_task "IsScared" {
    method "Become Scared" {
        preconditions: [mood == Mood::Scared, proximity_alert == true]
        subtasks: [
            PrepareToFlee,
            MoveToNextDestination,
            IsScared,
        ]
    }
    method "Cowering scared" {
        preconditions: [mood == Mood::Scared]
        subtasks: [Cower]
    }
}

primitive_task "Chill" {
    operator: WaitOperator
}

primitive_task "BecomeScared" {
    operator: BecomeScaredOperator
    effects: [mood = Mood::Scared]
}

primitive_task "Cower" {
    preconditions: [mood == Mood::Scared]
    operator: CowerOperator
}

primitive_task "Pause" {
    operator: WaitOperator
}

primitive_task "PrepareToFlee" {
    operator: PrepareToFleeOperator(player_location)
    effects: [mood = Mood::Scared]
}

primitive_task "Celebrate" {
    operator: SpinOperator
    effects: [coins_collected = 0]
}

primitive_task "PickDestination" {
    operator: PickDestinationOperator
    // not sure how to represent this... we can't set to Some without a value.
    // effects: [next_destination = Some]
}

primitive_task "MoveToNextDestination" {
    operator: MoveToOperator(next_destination)
    expected_effects: [proximity_alert = false]
}

primitive_task "MoveToCoin" {
    preconditions: [coin_location != None]
    operator: MoveToOperator(coin_location)
}